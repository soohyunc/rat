dnl UCL RAT configure script.
dnl $Header$
dnl
dnl Process this file with GNU autoconf to generate a configure script.

AC_INIT(converter.c)

AC_CANONICAL_SYSTEM

AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB

AC_HEADER_STDC
AC_C_CONST
AC_TYPE_SIZE_T

# Check for standard size types.  The defaults are only valid on some
# systems so we hope that <inttypes.h> exists when they're wrong.

AC_CHECK_TYPE(uint8_t, unsigned char)
AC_CHECK_TYPE(uint16_t, unsigned short)
AC_CHECK_TYPE(uint32_t, unsigned long)
AC_CHECK_TYPE(int8_t, signed char)
AC_CHECK_TYPE(int16_t, short)
AC_CHECK_TYPE(int32_t, long)
AC_CHECK_TYPE(int64_t, long long)

# Audio device selection,  derived from host type.  Could be smart and
# probe what's there...
AUDIOINC=
AUDIOLIB=
AUDIOOBJ=

case "$target" in
*solaris*)
	SUNAUDIOHOME="/usr/demo/SOUND"
	O1KHOME="/opt/SUNWo1kp"
	AC_ARG_WITH(sun-audio,    [  --with-sun-audio=DIR    specify Sun audio support home], SUNAUDIOHOME=$withval)
	AUDIOINC="$AUDIOINC -I$SUNAUDIOHOME/include"
	AUDIOLIB="$AUDIOLIB -L$SUNAUDIOHOME/lib -lsound"
	AUDIOOBJ="$AUDIOOBJ auddev_sparc.o" 
	AC_DEFINE(HAVE_SPARC_AUDIO)
	AC_ARG_WITH(osprey-audio, [  --with-osprey=DIR       specify Osprey (Sunvideo Plus PCI) support home], O1KHOME=$withval)
	AC_CHECK_HEADER($O1KHOME/include/oti_audio_device.h,
		[AUDIOINC="$AUDIOINC -I$O1KHOME/include"
		 AUDIOOBJ="$AUDIOOBJ auddev_osprey.o"
		 AC_DEFINE(HAVE_OSPREY_AUDIO)])
	;;
*linux*)
	AUDIOOBJ="$AUDIOOBJ auddev_oss.o"
	AC_DEFINE(HAVE_OSS_AUDIO)
	;;
*freebsd*)
	# PC Speaker device on FreeBSD
	AC_CHECK_HEADERS(machine/pcaudioio.h)
	# Luigi's driver
	AUDIOOBJ="$AUDIOOBJ auddev_luigi.o"
	AC_DEFINE(HAVE_LUIGI_AUDIO)
	;;
*netbsd*)
	AUDIOOBJ="$AUDIOBJ auddev_netbsd.o"
	AC_DEFINE(HAVE_NETBSD_AUDIO)
	;;
*irix*)
	AUDIOLIB="$AUDIOLIB -laudio"
	AUDIOOBJ="$AUDIOOBJ auddev_sgi.o"
	AC_DEFINE(HAVE_SGI_AUDIO)
	;;
*hpux*)
	AUDIOLIB="$AUDIOLIB -lAlib"
	AUDIOOBJ="$AUDIOOBJ auddev_hpux_raw.o"
	AC_DEFINE(HAVE_HP_AUDIO)
	;;
esac

AC_SUBST(AUDIOOBJ)
AC_SUBST(AUDIOINC)
AC_SUBST(AUDIOLIB)

AC_CONFIG_HEADER(config.h)

AC_OUTPUT(Makefile)
